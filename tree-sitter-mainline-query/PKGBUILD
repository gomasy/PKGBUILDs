# Maintainer: Gomasy <nyan@gomasy.jp>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: ObserverOfTime <chronobserver@disroot.org>

_pkg=tree-sitter-query
pkgname=tree-sitter-mainline-query
pkgver=0.7.0
pkgrel=1
pkgdesc='TS query grammar for tree-sitter'
arch=(x86_64 aarch64)
url=https://github.com/tree-sitter-grammars/tree-sitter-query
license=(Apache-2.0)
makedepends=(
  git
  tree-sitter-mainline-cli
)
optdepends=('tree-sitter-mainline: core library')
provides=("lib${_pkg}.so")
conflicts=(tree-sitter-query)
source=("git+$url.git#tag=v$pkgver")
b2sums=('7a3251623d2470d0040cabcc63f3913bb881bcce8b1cba96543030bfb1f22ee2501da4d8a4855106b2830c56a535c56a4b9e8d1e2632d70833672ea1f973f857')

prepare() {
  cd $_pkg
  tree-sitter generate src/grammar.json
}

build() {
  cd $_pkg
  make PREFIX=/usr PARSER_URL=$url
}

package() {
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib${_pkg}.so \
    "$pkgdir"/usr/lib/tree_sitter/${_pkg#tree-sitter-}.so

  cd $_pkg
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
