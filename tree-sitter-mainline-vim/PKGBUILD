# Maintainer: Gomasy <nyan@gomasy.jp>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: ObserverOfTime <chronobserver@disroot.org>

_pkg=tree-sitter-vim
pkgname=tree-sitter-mainline-vim
pkgver=0.7.0
pkgrel=1
pkgdesc='Vimscript grammar for tree-sitter'
arch=(x86_64 aarch64)
url=https://github.com/tree-sitter-grammars/tree-sitter-vim
license=(MIT)
makedepends=(
  git
  tree-sitter-mainline-cli
)
optdepends=('tree-sitter-mainline: core library')
provides=("lib${_pkg}.so")
conflicts=(tree-sitter-vim)
source=("git+$url.git#tag=v$pkgver")
b2sums=('9d6b56a784adf29c9395eb212820d3e007b4ba98939f4debcb9856748fce90044911895a00deda48902dc5c154e49b189c654fb44f92e44db513ce31b2ec61f0')

prepare() {
  cd $_pkg
  tree-sitter generate --no-bindings src/grammar.json
}

build() {
  cd $_pkg
  make PREFIX=/usr PARSER_URL=$url
}

package() {
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib${_pkg}.so \
    "$pkgdir"/usr/lib/tree_sitter/${_pkg#tree-sitter-}.so

  cd $_pkg
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
