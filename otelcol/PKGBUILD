# Maintainer: Gomasy <nyan@gomasy.jp>
# Contributor: Roger Coll <rogercoll at protonmail dot com>

pkgname=otelcol
pkgver=0.138.0
pkgrel=1
epoch=
pkgdesc="OpenTelemetry Collector Local binary"
arch=('x86_64' 'aarch64')
url="https://github.com/open-telemetry/opentelemetry-collector"
license=('APACHE')
groups=('open-telemetry')
makedepends=(
    go
    git
    systemd
)
backup=(
    etc/$pkgname/$pkgname.conf
    etc/$pkgname/config.yaml
)
install=$pkgname.install
source=(
    "$pkgname.service"
    "$pkgname.conf"
    "config.yaml"
    "builder-config.yaml"
)
source_x86_64=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_amd64")
source_aarch64=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_arm64")
sha512sums=('351cb7da00ae09559ca3e48932c1f0e0a8ccd4f1bff982ce2ce8ab3b50b5eec3715f2e21c9c12d04568f6b764588a92f4641c741b148080b0222e591fb691102'
            'f51d0e59bcdcf0c1f871de1667ed15f3f500cde6aabe78d8a596c238b8a1a1daa59831a203756ac372e5c753c0db75786612c93b65b99542c2a0ffcb5023e1e4'
            '50f13556ccce7464544bc48f1f9e23a074fb13fe433a0142e6694286791d85254532ba4751b8d615052dfdc4bb69556de8b4f67b9eeb2d2acee0f25bc29fad8f'
            'd677d17610d693d5f20c2de8adf0975e013b8adf32ef084d3b5c13a60a88a15433c66beaa2601111e58344aef5b5ba9e7e2860d8f73654e56cc0122b7e658aaf')
sha512sums_x86_64=('9e3fbc373bd4e24bc898e6d99f6ebd9a1200305403f43bf59355c5449f244bc478f1fe8089f45cb5f8625d0518ef33eb9bcd76fba30ba4c8f36e2460644147f0')
sha512sums_aarch64=('b2b8fa0fe8e2dd5aaac236e2caa06d757f2ecc277f63135dbbe20ce902c9c1ed9e2cad5a3ea0e3b8ad58cde118eb52c7bbba40f95efeb268814393284c097dd2')

prepare() {
    chmod +x ocb
}

build() {
    ./ocb --config=builder-config.yaml
}

package() {
    install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname/$pkgname.conf"
    install -Dm644 config.yaml "$pkgdir/etc/$pkgname/config.yaml"
    install -Dm755 dist/$pkgname "$pkgdir/usr/bin/$pkgname"
}
