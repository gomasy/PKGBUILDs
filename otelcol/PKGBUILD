# Maintainer: Gomasy <nyan@gomasy.jp>
# Contributor: Roger Coll <rogercoll at protonmail dot com>

pkgname=otelcol
pkgver=0.125.0
pkgrel=1
epoch=
pkgdesc="OpenTelemetry Collector Local binary"
arch=('x86_64' 'aarch64')
url="https://github.com/open-telemetry/opentelemetry-collector"
license=('APACHE')
groups=('open-telemetry')
makedepends=(
    go
    git
    systemd
)
backup=(
    etc/$pkgname/$pkgname.conf
    etc/$pkgname/config.yaml
)
install=$pkgname.install
source=(
    "$pkgname.service"
    "$pkgname.conf"
    "config.yaml"
    "builder-config.yaml"
)
source_x86_64=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_amd64")
source_aarch64=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_arm64")
sha512sums=('351cb7da00ae09559ca3e48932c1f0e0a8ccd4f1bff982ce2ce8ab3b50b5eec3715f2e21c9c12d04568f6b764588a92f4641c741b148080b0222e591fb691102'
            'f51d0e59bcdcf0c1f871de1667ed15f3f500cde6aabe78d8a596c238b8a1a1daa59831a203756ac372e5c753c0db75786612c93b65b99542c2a0ffcb5023e1e4'
            '50f13556ccce7464544bc48f1f9e23a074fb13fe433a0142e6694286791d85254532ba4751b8d615052dfdc4bb69556de8b4f67b9eeb2d2acee0f25bc29fad8f'
            '394358090c5f9317b68ddf4a60b7ca32c240d65cd3f15dd82b2713a6e4efe5916853c1c04f09bb53fbeae3b6424136e895c3f3fa201c9d79c0bc1df8374201c6')
sha512sums_x86_64=('05232aef598721ea734e447cb6c88663c4b4725c55e7be3cb85190d49c5da3da74f24e6587acecfd1277d170ed991b0f3204722b2b9035806f97d97db14d995e')
sha512sums_aarch64=('e29a5a1e9410761b5fc5da4bfce55460f10fb234da46907f07bd1d3c644488380c475881bc6f4c4b88fb7dfce91df9de7676ee81f24d9680bf7d409071e4d9ea')

prepare() {
    chmod +x ocb
}

build() {
    ./ocb --config=builder-config.yaml
}

package() {
    install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname/$pkgname.conf"
    install -Dm644 config.yaml "$pkgdir/etc/$pkgname/config.yaml"
    install -Dm755 dist/$pkgname "$pkgdir/usr/bin/$pkgname"
}
