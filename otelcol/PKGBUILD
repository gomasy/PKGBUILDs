# Maintainer: Gomasy <nyan@gomasy.jp>
# Contributor: Roger Coll <rogercoll at protonmail dot com>

pkgname=otelcol
pkgver=0.128.0
pkgrel=1
epoch=
pkgdesc="OpenTelemetry Collector Local binary"
arch=('x86_64' 'aarch64')
url="https://github.com/open-telemetry/opentelemetry-collector"
license=('APACHE')
groups=('open-telemetry')
makedepends=(
    go
    git
    systemd
)
backup=(
    etc/$pkgname/$pkgname.conf
    etc/$pkgname/config.yaml
)
install=$pkgname.install
source=(
    "$pkgname.service"
    "$pkgname.conf"
    "config.yaml"
    "builder-config.yaml"
)
source_x86_64=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_amd64")
source_aarch64=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_arm64")
sha512sums=('351cb7da00ae09559ca3e48932c1f0e0a8ccd4f1bff982ce2ce8ab3b50b5eec3715f2e21c9c12d04568f6b764588a92f4641c741b148080b0222e591fb691102'
            'f51d0e59bcdcf0c1f871de1667ed15f3f500cde6aabe78d8a596c238b8a1a1daa59831a203756ac372e5c753c0db75786612c93b65b99542c2a0ffcb5023e1e4'
            '50f13556ccce7464544bc48f1f9e23a074fb13fe433a0142e6694286791d85254532ba4751b8d615052dfdc4bb69556de8b4f67b9eeb2d2acee0f25bc29fad8f'
            'c596967ddca6cf52572ac2c7bbdb7990f5ee58756adcc569f60444e6b71d671320ab36bced598ea9f521a1c415d232e4f147641d6f10fad49af5f0e9d3afe181')
sha512sums_x86_64=('0ce2fbabb4880f1786d74ee79cba94695d8321ef3e58b1291fd79c242c7f80619223477e3e606c343f7b7a2c070ded97dd42910759719fb012dd502ecb24740a')
sha512sums_aarch64=('1f6ff6c3ed668e828d640c76c39b9ad64b1325f022cf578492c6d953167a551ce0c26be6ea672e9441dfb1caafe46e983467f81a3f41287714337e42498b3e5b')

prepare() {
    chmod +x ocb
}

build() {
    ./ocb --config=builder-config.yaml
}

package() {
    install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname/$pkgname.conf"
    install -Dm644 config.yaml "$pkgdir/etc/$pkgname/config.yaml"
    install -Dm755 dist/$pkgname "$pkgdir/usr/bin/$pkgname"
}
