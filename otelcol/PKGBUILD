# Maintainer: Gomasy <nyan@gomasy.jp>
# Contributor: Roger Coll <rogercoll at protonmail dot com>

pkgname=otelcol
pkgver=0.144.0
pkgrel=1
epoch=
pkgdesc="OpenTelemetry Collector Local binary"
arch=('x86_64' 'aarch64')
url="https://github.com/open-telemetry/opentelemetry-collector"
license=('APACHE')
groups=('open-telemetry')
makedepends=(
    go
    git
    systemd
)
backup=(
    etc/$pkgname/$pkgname.conf
    etc/$pkgname/config.yaml
)
install=$pkgname.install
source=(
    "$pkgname.service"
    "$pkgname.conf"
    "config.yaml"
    "builder-config.yaml"
)
source_x86_64=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_amd64")
source_aarch64=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_arm64")
sha512sums=('351cb7da00ae09559ca3e48932c1f0e0a8ccd4f1bff982ce2ce8ab3b50b5eec3715f2e21c9c12d04568f6b764588a92f4641c741b148080b0222e591fb691102'
            'f51d0e59bcdcf0c1f871de1667ed15f3f500cde6aabe78d8a596c238b8a1a1daa59831a203756ac372e5c753c0db75786612c93b65b99542c2a0ffcb5023e1e4'
            '50f13556ccce7464544bc48f1f9e23a074fb13fe433a0142e6694286791d85254532ba4751b8d615052dfdc4bb69556de8b4f67b9eeb2d2acee0f25bc29fad8f'
            '37ef178450d7c7a0b7d2a5a5669c80abf92f0b059e99a6a69ef49e38fea5955618f23f0c2d8a1c99cd799b9d2f7cbc827a547e7e74a12fef0858e2e32aa341eb')
sha512sums_x86_64=('2b52e1598fdd25f3ea499ff4128aa140f72d67cf39bd9eb8e41e45ed57a82124db491dc2683c4a9236b9beb65a0bb249898c1283ddcf36cd6f8ad7df202c64da')
sha512sums_aarch64=('79099b334673f2f7cf3a07bfcc4cccbf84c24ef18b2cf82d090e15761f0b51a70ce9ac34215d9c094ffa9aeb923516039ab24259bba9df3750226cf83ae3468d')

prepare() {
    chmod +x ocb
}

build() {
    ./ocb --config=builder-config.yaml
}

package() {
    install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname/$pkgname.conf"
    install -Dm644 config.yaml "$pkgdir/etc/$pkgname/config.yaml"
    install -Dm755 dist/$pkgname "$pkgdir/usr/bin/$pkgname"
}
