# Maintainer: Roger Coll <rogercoll at protonmail dot com>

pkgname=otelcol
pkgver=0.111.0
pkgrel=1
epoch=
pkgdesc="OpenTelemetry Collector Local binary"
arch=('x86_64')
url="https://github.com/open-telemetry/opentelemetry-collector"
license=('APACHE')
groups=('open-telemetry')
makedepends=(
    go
    git
    systemd
)
backup=(
    etc/$pkgname/$pkgname.conf
    etc/$pkgname/config.yaml
)
install=$pkgname.install
source=("ocb::${url}-releases/releases/download/cmd%2Fbuilder%2Fv${pkgver}/ocb_${pkgver}_linux_amd64"
    "$pkgname.service"
    "$pkgname.conf"
    "config.yaml"
    "builder-config.yaml"
)
sha512sums=('3d875436dae2d58d76e21f50e4b3856dac22e97c983ab7514a4c6e85aeeea413694686afdd74d52e65dbc57593c77f84d230c265a883bb84be09832a296f8120'
            '351cb7da00ae09559ca3e48932c1f0e0a8ccd4f1bff982ce2ce8ab3b50b5eec3715f2e21c9c12d04568f6b764588a92f4641c741b148080b0222e591fb691102'
            'f51d0e59bcdcf0c1f871de1667ed15f3f500cde6aabe78d8a596c238b8a1a1daa59831a203756ac372e5c753c0db75786612c93b65b99542c2a0ffcb5023e1e4'
            '11e66d39858f16db3c495e41a15fbbf4c84a2046c55c4fe8f647ecf6cbeebb64464c371f6aa2809d150bf32873e006157d6bb4ed912968057fa55e96722f6701'
            '185f7779a17a5fcebd1d2858dfc58927ef3bf31ccf009f2f4b6c0f66a63034bb6157b756fb50abe4855122fd0f6954ae50bcf6ae2ceaa2688fe2570ecee4f5c8')


build() {
    chmod +x ocb
    ./ocb --config=builder-config.yaml
}

package() {
    install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname/$pkgname.conf"
    install -Dm644 config.yaml "$pkgdir/etc/$pkgname/config.yaml"
    install -Dm755 dist/$pkgname "$pkgdir/usr/bin/$pkgname"
}
