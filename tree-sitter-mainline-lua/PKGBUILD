# Maintainer: Gomasy <nyan@gomasy.jp>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: ObserverOfTime <chronobserver@disroot.org>

_pkg=tree-sitter-lua
pkgname=tree-sitter-mainline-lua
pkgver=0.4.0
pkgrel=1
pkgdesc='Lua grammar for tree-sitter'
arch=(x86_64 aarch64)
url=https://github.com/tree-sitter-grammars/tree-sitter-lua
license=(MIT)
makedepends=(
  git
  tree-sitter-mainline-cli
)
provides=("lib${_pkg}.so")
optdepends=('tree-sitter-mainline: core library')
conflicts=(tree-sitter-lua)
source=("git+$url.git#tag=v$pkgver")
b2sums=('3da021f6da12d4fec4bba516f1a050203f0ba2cb0faed750f5154c17fe99e84c57a2b4ae1d6df35db3a555e81c1ca6572ba84470b712060eed74b8d1d74048b3')

prepare() {
  cd $_pkg
  tree-sitter generate --no-bindings src/grammar.json
}

build() {
  cd $_pkg
  make PREFIX=/usr PARSER_URL=$url
}

package() {
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib${_pkg}.so \
    "$pkgdir"/usr/lib/tree_sitter/${_pkg#tree-sitter-}.so

  cd $_pkg
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.md
}
