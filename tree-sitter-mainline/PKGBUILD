# Maintainer: Gomasy <nyan@gomasy.jp>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=tree-sitter
pkgname=(
  tree-sitter-mainline
  tree-sitter-mainline-cli
)
pkgver=0.26.5.r117.ga21ee0271
pkgrel=1
arch=('x86_64' 'aarch64')
url="https://tree-sitter.github.io/tree-sitter"
license=('MIT')
makedepends=('cmake' 'git' 'rust')
options=(!lto) # Needed for CLI build
source=("git+https://github.com/tree-sitter/tree-sitter.git")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase

  _tag=$(git tag -l --sort -v:refname | grep -E '^v?[0-9\.]+$' | head -n1)
  _rev=$(git rev-list --count $_tag..HEAD)
  _hash=$(git rev-parse --short HEAD)
  printf "%s.r%s.g%s" "$_tag" "$_rev" "$_hash" | sed 's/^v//'
}

prepare() {
  cd $pkgbase/crates/cli
  cargo fetch --locked --target "$(rustc --print host-tuple)"
}

build() {
  cd $pkgbase
  cmake -S . -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON # Enable LTO
  cmake --build build

  cd crates/cli
  cargo build --release --locked --offline

  for completion in bash elvish fish nushell zsh; do
    cargo run --frozen --release -- \
      complete --shell $completion > $completion-completions
  done
}

package_tree-sitter-mainline() {
  pkgdesc='Incremental parsing library'
  provides=('tree-sitter=$pkgver' 'libtree-sitter.so')
  conflicts=('tree-sitter')

  cd $pkgbase
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgbase
}

package_tree-sitter-mainline-cli() {
  pkgdesc='CLI tool for developing, testing, and using Tree-sitter parsers'
  depends=('gcc-libs')
  optdepends=('nodejs: for the generate subcommand')
  conflicts=('tree-sitter-cli')

  cd $pkgbase
  install -Dt "$pkgdir"/usr/bin target/release/$pkgbase
  install -Dm644 -t "$pkgdir"/usr/share/licenses/${pkgbase}-cli LICENSE

  cd crates/cli
  install -Dm644 bash-completions "$pkgdir"/usr/share/bash-completion/completions/$pkgbase
  install -Dm644 elvish-completions "$pkgdir"/usr/share/elvish/lib/$pkgbase.elv
  install -Dm644 fish-completions "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgbase.fish
  install -Dm644 nushell-completions "$pkgdir"/usr/share/nushell/vendor/autoload/$pkgbase.nu
  install -Dm644 zsh-completions "$pkgdir"/usr/share/zsh/site-functions/_$pkgbase
}
